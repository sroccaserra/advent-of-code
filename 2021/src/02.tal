~src/macros.tal

( devices )

|00 @System     [ &vector $2 &wst $1 &rst $1 &pad $4 &r $2 &g $2 &b $2 &debug $1 &halt $1 ]
|10 @Console    [ &vector $2 &read $1 &pad $5 &write $1 &error $1 ]
|20 @Screen     [ &vector $2 &width $2 &height $2 &pad $2 &x $2 &y $2 &addr $2 &pixel $1 &sprite $1 ]
|80 @Controller [ &vector $2 &button $1 &key $1 ]
|a0 @File       [ &vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2 ]

( zero page )

|00
@buffer [ &length $1 &data $f ]
@state  [ &hpos-1 $2 &depth-1 $2 ] [ &hpos-2 $2 &depth-2 $4 ]

( init )

|0100 @program
    #0000 .state/hpos-1 STZ2
    #0000 .state/depth-1 STZ2

    #0000 .state/hpos-2 STZ2
    #0000 #0000 .state/depth-2 STZ4

    ;filename ;stream JSR2

    .state/hpos-1 LDZ2
    .state/depth-1 LDZ2
    ;mul16 JSR2
    ;print-dec-32 JSR2 LF

    #0000 .state/hpos-2 LDZ2
    .state/depth-2 LDZ4
    ;mul32 JSR2
    ;print-dec-32 JSR2 LF

    HALT
BRK

@stream ( addr* -- )
    .File/name DEO2
    #0001 .File/length DEO2

    &loop
    ;&char .File/read DEO2
    ;&char LDA EOL? ,&eol JCN

    ;&char LDA .buffer/length LDZ .buffer/data + STZ
    .buffer/length LDZk INC SWP STZ
    ,&continue JMP

    &eol
    #00 .buffer/length LDZ .buffer/data + STZ
    ;on-line JSR2
    #00 .buffer/length STZ

    &continue
    .File/success DEI2 #0000 NEQ2 ,&loop JCN
RTN
&char $1

@on-line ( -- )
    .buffer/length LDZ #00 EQU ;&end JCN2

    [ .buffer/data ] [ .buffer/length LDZ #01 - ] + LDZ
    #30 - #00 SWP ( value* )

    .buffer/data LDZ ( value* command^ )
    DUP LIT 'f EQU ,&forward JCN
    DUP LIT 'u EQU ,&up JCN
    DUP LIT 'd EQU ,&down JCN

    P< "unknown #20 "command #20 >P ( command^ ) EMIT LF
    HALT
    BRK

    &forward POP
    DUP2
    .state/hpos-1 LDZ2 ++ .state/hpos-1 STZ2
    DUP2
    .state/hpos-2 LDZ2 ++ .state/hpos-2 STZ2
    ( aim formula is the same as depth-1 formula )
    .state/depth-1 LDZ2 ;mul16 JSR2 .state/depth-2 LDZ4 ;add32 JSR2 .state/depth-2 STZ4
    ,&end JMP

    &up POP
    .state/depth-1 LDZ2 SWP2 -- .state/depth-1 STZ2
    ,&end JMP

    &down POP
    .state/depth-1 LDZ2 ++ .state/depth-1 STZ2

    &end
RTN

~lib/math32.tal
~src/common.tal

@filename "src/02.in 00
